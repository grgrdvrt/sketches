<html>
    <head>
        <title>noise</title>
        <style>
         body { margin: 0; }
         canvas { width: 100%; height: 100% }
        </style>
    </head>
    <body>
        <script id="vertexShader" type="x-shader/x-vertex">
         //
         // Description : Array and textureless GLSL 2D simplex noise function.
         //      Author : Ian McEwan, Ashima Arts.
         //  Maintainer : stegu
         //     Lastmod : 20110822 (ijm)
         //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
         //               Distributed under the MIT License. See LICENSE file.
         //               https://github.com/ashima/webgl-noise
         //               https://github.com/stegu/webgl-noise
         //

         vec3 mod289(vec3 x) {
             return x - floor(x * (1.0 / 289.0)) * 289.0;
         }

         vec2 mod289(vec2 x) {
             return x - floor(x * (1.0 / 289.0)) * 289.0;
         }

         vec3 permute(vec3 x) {
             return mod289(((x*34.0)+1.0)*x);
         }

         float snoise(vec2 v){
             const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                                 0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                                -0.577350269189626,  // -1.0 + 2.0 * C.x
                                 0.024390243902439); // 1.0 / 41.0
             // First corner
             vec2 i  = floor(v + dot(v, C.yy) );
             vec2 x0 = v -   i + dot(i, C.xx);

             // Other corners
             vec2 i1;
             //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
             //i1.y = 1.0 - i1.x;
             i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
             // x0 = x0 - 0.0 + 0.0 * C.xx ;
             // x1 = x0 - i1 + 1.0 * C.xx ;
             // x2 = x0 - 1.0 + 2.0 * C.xx ;
             vec4 x12 = x0.xyxy + C.xxzz;
             x12.xy -= i1;

             // Permutations
             i = mod289(i); // Avoid truncation effects in permutation
             vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
                             + i.x + vec3(0.0, i1.x, 1.0 ));

             vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
             m = m*m ;
             m = m*m ;

             // Gradients: 41 points uniformly over a line, mapped onto a diamond.
             // The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

             vec3 x = 2.0 * fract(p * C.www) - 1.0;
             vec3 h = abs(x) - 0.5;
             vec3 ox = floor(x + 0.5);
             vec3 a0 = x - ox;

             // Normalise gradients implicitly by scaling m
             // Approximation of: m *= inversesqrt( a0*a0 + h*h );
             m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

             // Compute final noise value at P
             vec3 g;
             g.x  = a0.x  * x0.x  + h.x  * x0.y;
             g.yz = a0.yz * x12.xz + h.yz * x12.yw;
             return 130.0 * dot(m, g);
         }

         #define PHONG


         varying vec3 vViewPosition;

         #ifndef FLAT_SHADED

         varying vec3 vNormal;

         #endif

         #include <common>
         #include <uv_pars_vertex>
         #include <uv2_pars_vertex>
         #include <displacementmap_pars_vertex>
         #include <envmap_pars_vertex>
         #include <color_pars_vertex>
         #include <morphtarget_pars_vertex>
         #include <skinning_pars_vertex>
         #include <shadowmap_pars_vertex>
         #include <logdepthbuf_pars_vertex>
         #include <clipping_planes_pars_vertex>

         const vec3 off = vec3(-1.0, 0.0, 1.0);


         uniform float time;
         varying vec3 vPos;

         void main() {
             vec2 samplePosition = 2.0 * vec2(position.x + 0.002 * time, position.y);
             float attenuation = 0.1 * clamp(1.0 - length(2.0 * position), 0.0, 1.0);
             float displacement = attenuation * snoise(samplePosition);

             #include <uv_vertex>
             #include <uv2_vertex>
             #include <color_vertex>

             #include <beginnormal_vertex>

             vec3 delta = 0.01 * off;
             float left = snoise(samplePosition + delta.xy);
             float right = snoise(samplePosition + delta.zy);
             float top = snoise(samplePosition + delta.yx);
             float bottom = snoise(samplePosition + delta.yz);
             vec2 size = vec2(attenuation, 0.0);
             vec3 va = normalize(vec3(size.xy, right - left));
             vec3 vb = normalize(vec3(size.yx, bottom - top));
             objectNormal = cross(va, vb);

             #include <morphnormal_vertex>
             #include <skinbase_vertex>
             #include <skinnormal_vertex>
             #include <defaultnormal_vertex>

             #ifndef FLAT_SHADED // Normal computed with derivatives when FLAT_SHADED

             vNormal = normalize( transformedNormal );

             #endif

             #include <begin_vertex>

             transformed += vec3(0.0, 0.0, displacement);

             #include <displacementmap_vertex>
             #include <morphtarget_vertex>
             #include <skinning_vertex>
             #include <project_vertex>
             #include <logdepthbuf_vertex>
             #include <clipping_planes_vertex>

             vViewPosition = - mvPosition.xyz;
             vPos = transformed;

             #include <worldpos_vertex>
             #include <envmap_vertex>
             #include <shadowmap_vertex>

         }
        </script>

        <script id="fragmentShader" type="x-shader/x-fragment">
         #define PHONG

         uniform vec3 diffuse;
         uniform vec3 emissive;
         uniform vec3 specular;
         uniform float shininess;
         uniform float opacity;

         #include <common>
         #include <packing>
         #include <color_pars_fragment>
         #include <uv_pars_fragment>
         #include <uv2_pars_fragment>
         #include <map_pars_fragment>
         #include <alphamap_pars_fragment>
         #include <aomap_pars_fragment>
         #include <lightmap_pars_fragment>
         #include <emissivemap_pars_fragment>
         #include <envmap_pars_fragment>
         #include <fog_pars_fragment>
         #include <bsdfs>
         #include <lights_pars>
         #include <lights_phong_pars_fragment>
         #include <shadowmap_pars_fragment>
         #include <bumpmap_pars_fragment>
         #include <normalmap_pars_fragment>
         #include <specularmap_pars_fragment>
         #include <logdepthbuf_pars_fragment>
         #include <clipping_planes_pars_fragment>

         uniform float time;
         varying vec3 vPos;
         void main() {

             #include <clipping_planes_fragment>

             vec4 diffuseColor = vec4( diffuse, opacity );
             ReflectedLight reflectedLight = ReflectedLight( vec3( 0.0 ), vec3( 0.0 ), vec3( 0.0 ), vec3( 0.0 ) );
             vec3 totalEmissiveRadiance = emissive;

             #include <logdepthbuf_fragment>
             #include <map_fragment>
             #include <color_fragment>
             #include <alphamap_fragment>
             #include <alphatest_fragment>
             #include <specularmap_fragment>
             #include <normal_flip>
             #include <normal_fragment>
             #include <emissivemap_fragment>

             // accumulation
             #include <lights_phong_fragment>
             #include <lights_template>

             // modulation
             #include <aomap_fragment>

             vec3 outgoingLight = reflectedLight.directDiffuse + reflectedLight.indirectDiffuse + reflectedLight.directSpecular + reflectedLight.indirectSpecular + totalEmissiveRadiance;

             #include <envmap_fragment>

             float attenuation = clamp(1.0 - length(2.0 * vPos), 0.0, 1.0);
             gl_FragColor = vec4( attenuation * outgoingLight, diffuseColor.a );

             #include <premultiplied_alpha_fragment>
             #include <tonemapping_fragment>
             #include <encodings_fragment>
             #include <fog_fragment>

         }
        </script>
        <script src="../libs/three.min.js"></script>
        <script src="script.js"></script>
    </body>
</html>
